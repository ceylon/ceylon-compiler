rem User HOME

set CEYLON_VERSION=@VERSION@

if "%HOME" == "" ( 
    set "HOME=%USERPROFILE%" 
)

rem Find CEYLON_HOME
if not "%CEYLON_HOME%" == "" goto ceylon_home_set
rem Get absolute value for CEYLON_HOME
pushd "%~dp0.."
set "CEYLON_HOME=%CD%"
popd
:ceylon_home_set

rem Set CEYLON_REPO if not already set
if "%CEYLON_REPO%" == "" (
    rem Try the destribution repo
    set "CEYLON_REPO=%CEYLON_HOME%\repo"
)

rem If it does not exist let's use the user repo
if not exist "%CEYLON_REPO%" (
    @echo "Could not find distribution repo, using %HOME%\.ceylon\repo"
    set "CEYLON_REPO=%HOME%\.ceylon\repo"
)

rem Find Java
call %~dp0\java.bat

if "%JAVA_HOME%" == "" (
    set "exit=true"
    exit /b 1
)

set "JAVA=%JAVA_HOME%\bin\java.exe"

rem Check that Java executable actually exists
if not exist "%JAVA%" (
    @echo "Cannot find java.exe at %JAVA%, check that your JAVA_HOME variable is pointing to the right place"
    set "exit=true"
    exit /b 1
)

rem Process -cp and other args
set USER_CP=
set ARGS=

:loop_args
if "%~1" == "" goto :done_args

if "%~1" == "-cp" (
    set "USER_CP=%~2"
    shift
) else if "%~1" == "-classpath" (
    set "USER_CP=%~2"
    shift
) else (
    set "ARGS=%ARGS% %1"
)

shift
goto loop_args

:done_args

rem MINIMAL_CP are classes required by the tools to run
set "MINIMAL_CP=%CEYLON_REPO%\com\redhat\ceylon\compiler\java\%CEYLON_VERSION%\com.redhat.ceylon.compiler.java-%CEYLON_VERSION%.jar"
set "MINIMAL_CP=%MINIMAL_CP%;%CEYLON_REPO%\com\redhat\ceylon\typechecker\%CEYLON_VERSION%\com.redhat.ceylon.typechecker-%CEYLON_VERSION%.jar"
set "MINIMAL_CP=%MINIMAL_CP%;%CEYLON_REPO%\com\redhat\ceylon\module-resolver\%CEYLON_VERSION%\com.redhat.ceylon.module-resolver-%CEYLON_VERSION%.jar"
set "MINIMAL_CP=%MINIMAL_CP%;%CEYLON_HOME%\lib\antlr-3.4-complete.jar"
set "MINIMAL_CP=%MINIMAL_CP%;%CEYLON_HOME%\lib\sardine-314.jar"
set "MINIMAL_CP=%MINIMAL_CP%;%CEYLON_HOME%\lib\slf4j-api-1.6.1.jar"
set "MINIMAL_CP=%MINIMAL_CP%;%CEYLON_HOME%\lib\slf4j-simple-1.6.1.jar"
set "MINIMAL_CP=%MINIMAL_CP%;%CEYLON_HOME%\lib\httpclient-4.1.1.jar"
set "MINIMAL_CP=%MINIMAL_CP%;%CEYLON_HOME%\lib\httpcore-4.1.1.jar"
set "MINIMAL_CP=%MINIMAL_CP%;%CEYLON_HOME%\lib\commons-logging-1.1.1.jar"
set "MINIMAL_CP=%MINIMAL_CP%;%CEYLON_HOME%\lib\commons-codec-1.4.jar"
set "MINIMAL_CP=%MINIMAL_CP%;%CEYLON_HOME%\lib\jandex-1.0.3.Final.jar"
